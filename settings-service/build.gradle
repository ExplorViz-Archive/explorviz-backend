plugins {
	id "application"
	id "com.github.johnrengelman.shadow" version "4.0.4"
	id "com.github.spotbugs" version "1.6.10"
}

// Force new Dependencies 
configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

task integrationTest(type: Test) {
  useJUnitPlatform{
    excludeTags 'slow'
    includeEngines 'junit-jupiter'
  }
  failFast = true
  description = 'Runs integration tests.'
  group = 'verification'

  testClassesDirs = sourceSets.intTest.output.classesDirs
  classpath = sourceSets.intTest.runtimeClasspath
  mustRunAfter assemble
}

// integrationTest.dependsOn assemble
//    this will only assemble the current service but if it depends on another service
//    the build will fail as the integration test stage is executed before other modules are
//    assembled.

// check.dependsOn integrationTest

dockerCompose {
  runAll {
    buildBeforeUp = true
    useComposeFiles = ['docker-compose-integration-test.yml']
    startedServices = ['user-service', 'mongo-user', 'zookeeper', 'kafka', 'settings-service', 'mongo-user', 'mongo-settings']
    isRequiredBy(this.tasks.getByName("integrationTest"))
    removeContainers = true
  }
}

dependencies {
		
	// MongoDB ORM
	implementation 'xyz.morphia.morphia:core:1.4.0'
	
	// Swagger
  	implementation group: 'io.swagger.core.v3', name: 'swagger-core', version: '2.0.8'
  	implementation group: 'io.swagger.core.v3', name: 'swagger-jaxrs2', version: '2.0.8' 
	
	implementation project(':settings-service:settings-service-model')
	
	// ExplorViz Shared Dependencies
	if(useRemoteSharedProject.toBoolean()) {
    	implementation "com.github.explorviz.explorviz-backend-shared:user:${sharedProjectVersion}"
    	implementation "com.github.explorviz.explorviz-backend-shared:config-injection:${sharedProjectVersion}"
		implementation "com.github.explorviz.explorviz-backend-shared:security:${sharedProjectVersion}"
		implementation "com.github.explorviz.explorviz-backend-shared:exception-handling:${sharedProjectVersion}"
		implementation "com.github.explorviz.explorviz-backend-shared:common-concerns:${sharedProjectVersion}" 
		implementation "com.github.explorviz.explorviz-backend-shared:query:${sharedProjectVersion}"
  	}
  	else {
	    implementation project(':user')
    	implementation project(':config-injection')
    	implementation project(':security')
    	implementation project(':exception-handling')
    	implementation project(':common-concerns')
    	implementation project(':query')
  }


  intTestImplementation project(':user-service')    // For login
  intTestImplementation 'io.rest-assured:rest-assured:4.0.0'

}

assemble.dependsOn shadowJar
jar.enabled = false

jar {
  manifest {
    attributes(
      'Main-Class': 'net.explorviz.settings.server.main.Main'
    )
  }
}


application {
    mainClassName = 'net.explorviz.settings.server.main.Main'
}

shadowJar {
  archiveName = "explorviz-${project.name}.${extension}"
}

spotbugs {
  excludeFilter = file("../conf/spotbugs-exclude-filter.xml")
  reportLevel = "low"
  effort = "max"
  ignoreFailures = true
  toolVersion = '3.1.7'
}

// Per default XML reports for SpotBugs are generated
// Include this to generate HTML reports
tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    // Either HTML or XML reports can be activated
    html.enabled true
    xml.enabled false
  }
}

task spotbugs {
  group 'Quality Assurance'
  description 'Run SpotBugs'
  
  dependsOn 'spotbugsMain'
  dependsOn 'spotbugsTest'
}

artifacts {
  archives shadowJar
}
