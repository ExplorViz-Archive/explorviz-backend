plugins {
	id "application"
	id "org.sonarqube" version "2.7"
	id "com.github.johnrengelman.shadow" version "5.1.0"
	id "com.github.spotbugs" version "1.6.10"
	id "maven"
}

// Force new Dependencies 
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dockerCompose {
  landscapeIntegrationTests {
    buildBeforeUp = true
    useComposeFiles = ['docker-compose/docker-compose-integration-test.yml']
    isRequiredBy(this.tasks.getByName('integrationTest'))
    removeContainers = true
  }
}

repositories {
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
	mavenLocal()
}

dependencies {
  // Any .jar in libs
  implementation fileTree(dir: 'libs', include: '*.jar')
  
  implementation project(':landscape-service:landscape-model')
  implementation group: 'org.glassfish.hk2', name: 'hk2', version: '2.6.0'
  implementation group: 'com.github.jasminb', name: 'jsonapi-converter', version:'0.8'
  
  // ExplorViz Shared Dependencies
  if(useRemoteSharedProject.toBoolean()) {
  	implementation group: 'net.explorviz', name: 'config-injection', version: "${sharedProjectVersion}"
  	implementation group: 'net.explorviz', name: 'security', version: "${sharedProjectVersion}"
  	implementation group: 'net.explorviz', name: 'exception-handling', version: "${sharedProjectVersion}"
  	implementation group: 'net.explorviz', name: 'common-concerns', version: "${sharedProjectVersion}"
  }
  else {        
    implementation project(':config-injection')
    implementation project('::security')
    implementation project(':exception-handling')
    implementation project(':common-concerns')
  }

  implementation project(':landscape-service:landscape-model')
  implementation group: 'com.github.jasminb', name: 'jsonapi-converter', version:'0.8'
}

// disable api tests, since there are none
apiTest.enabled = false

assemble.dependsOn shadowJar
jar.enabled = false

jar {
  manifest {
    attributes(
      'Main-Class': 'net.explorviz.landscape.server.main.Main'
    )
  }
}

application {
    mainClassName = 'net.explorviz.landscape.server.main.Main'
}

shadowJar {
  baseName = "explorviz-${project.name}"
  classifier = null
}

// BEGIN Jitpack publishing
artifacts {
  archives shadowJar
}

archivesBaseName = shadowJar.baseName
// END Jitpack publishing

// Javadoc settings
javadoc {
  source = sourceSets.main.allJava
  destinationDir = file("./docs/")
  failOnError false
}

spotbugs {
  excludeFilter = file("../conf/spotbugs-exclude-filter.xml")
  reportLevel = "low"
  effort = "max"
  ignoreFailures = true
  toolVersion = '3.1.7'
}

// Per default XML reports for SpotBugs are generated
// Include this to generate HTML reports
tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    // Either HTML or XML reports can be activated
    html.enabled true
    xml.enabled false
  }
}

task spotbugs {
  group 'Quality Assurance'
  description 'Run SpotBugs'
  
  dependsOn 'spotbugsMain'
  dependsOn 'spotbugsTest'
}

// START environment task
task updateEnvPropsInBuildDir {

	// run with:
	// .././gradlew run -Penvironment=production

	doLast {
		if (project.hasProperty("environment")) {
   			def environment = project.findProperty("environment")
   	
   			if(environment.equals("production")) {
   				println("Using the production properties file")
   				delete("${buildDir}/resources/main/explorviz.properties")
    		
    			copy { 
      				from "${buildDir}/resources/main" 
    				into "${buildDir}/resources/main"
    				rename "explorviz.production.properties", "explorviz.properties" 
    			}
    		
    			delete("${buildDir}/resources/main/explorviz.production.properties")        		
			}
		} else {
    		println("Using default properties file")
		}
	}
}

run.dependsOn updateEnvPropsInBuildDir
updateEnvPropsInBuildDir.shouldRunAfter classes
// END environment task
