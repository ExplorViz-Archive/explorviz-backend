apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'war'

buildscript {
  	repositories {
      	jcenter()
    } 
}

repositories {
  	jcenter()
  	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
	// include libraries in lib, e.g. the ExplorViz monitoring component
	compile fileTree(dir: 'libs', include: ['*.jar'])

	// Teetime Pipe and Filter Framework
	compile group: 'net.sourceforge.teetime', name: 'teetime', version: '3.0-SNAPSHOT'
	compile group: 'net.sourceforge.teetime-stages', name: 'teetime-stages', version: '3.0-SNAPSHOT'
	
	// Kieker Monitoring Framework
	compile group: 'net.kieker-monitoring', name: 'kieker', version: '1.14-SNAPSHOT'
	
	// SL4J
	compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
	
    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// ##### BEGIN Jitpack section #####

// War plugin typically disables jar build
assemble.dependsOn jar

artifacts {
	archives file: file('libs/explorviz-monitoring.jar'), name: 'explorviz-monitoring', type: 'jar'
  	archives jar
  	archives war {
		classifier = 'deployment'
	}
}

jar {
  baseName = project.name 
}

// ##### END Jitpack section #####

install {
  repositories {
    mavenInstaller {
      addFilter('explorviz-monitoring') {artifact, file ->
        artifact.name == 'explorviz-monitoring'
      }
      addFilter('explorviz-backend-kiekeradapter') {artifact, file ->
        artifact.name == 'explorviz-backend-kiekeradapter'
      }
    }
  }
}