plugins {
	id "application"
	id "com.github.johnrengelman.shadow" version "4.0.4"
	id "com.github.spotbugs" version "1.6.10"
}

// Force new Dependencies 
configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

ext {	
	expSharedVersion = "${rootProject.ext.explorvizSharedVersion}"
}

dependencies {
  // ExplorViz Discovery Classes
  implementation "com.github.explorviz.explorviz-backend-shared:config-injection:${expSharedVersion}"

  // ExplorViz Discovery Classes
  implementation "com.github.explorviz.explorviz-backend-shared:discovery-common:${expSharedVersion}"
  
  // ExplorViz Discovery Classes
  implementation "com.github.explorviz.explorviz-backend-shared:security:${expSharedVersion}"
  
  // ExplorViz common exception handling
  implementation "com.github.explorviz.explorviz-backend-shared:exception-handling:${expSharedVersion}"
  
  implementation "com.github.explorviz.explorviz-backend-shared:common-concerns:${expSharedVersion}"
  
  // SSE context for injection
  implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-sse', version: '2.27'  
}

/*task run (type: JavaExec, dependsOn: classes){
	doLast {
        if (project.hasProperty("environment")) {
        	alex = project.hasProperty("environment")
            println("Performing release actions")
            println(alex)
        } else {
        	println("Using default properties file")
        }
    }
}*/


task updateDeploymentEnvironmentProperties {

	// run with:
	
	// .././gradlew run -Penvironment=production
  	
	doLast {
	    if (project.hasProperty("environment")) {
        	def environment = project.findProperty("environment")
        	
        	if(environment.equals("production")) {
        		println("Using the production properties file")
        		delete("${buildDir}/resources/main/explorviz.properties")
        		
        		copy { 
        			from "${buildDir}/resources/main" 
        			into "${buildDir}/resources/main"
        			rename "explorviz.production.properties", "explorviz.properties" 
        		}
        		
        		delete("${buildDir}/resources/main/explorviz.production.properties")        		
        	}
        } else {
        	println("Using default properties file")
        }
	}
}

run.dependsOn updateDeploymentEnvironmentProperties
updateDeploymentEnvironmentProperties.shouldRunAfter classes

assemble.dependsOn shadowJar
jar.enabled = false

jar {
  manifest {
    attributes(
      'Main-Class': 'net.explorviz.discovery.server.main.Main'
    )
  }
}

application {
    mainClassName = 'net.explorviz.discovery.server.main.Main'
}

shadowJar {
  archiveName = "explorviz-${project.name}.${extension}"
}

spotbugs {
  excludeFilter = file("../conf/spotbugs-exclude-filter.xml")
  reportLevel = "low"
  effort = "max"
  ignoreFailures = true
  toolVersion = '3.1.7'
}

// Per default XML reports for SpotBugs are generated
// Include this to generate HTML reports
tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    // Either HTML or XML reports can be activated
    html.enabled true
    xml.enabled false
  }
}

task spotbugs {
  group 'Quality Assurance'
  description 'Run SpotBugs'
  
  dependsOn 'spotbugsMain'
  dependsOn 'spotbugsTest'
}

artifacts {
  archives shadowJar
}
