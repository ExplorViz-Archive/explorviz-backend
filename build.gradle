plugins {
    id 'com.adarshr.test-logger' version '1.6.0' apply false
    id 'com.avast.gradle.docker-compose' version "0.9.4"
}

apply plugin: 'java'

allprojects {
  apply plugin: 'java-library'
  apply plugin: "eclipse"  

	java {
	    sourceCompatibility = JavaVersion.VERSION_11
	    targetCompatibility = JavaVersion.VERSION_11
	}
  
}

// defining project properties
def	projectVersion = 'v1.3.0'
def projectURL= 'https://www.explorviz.net'
def footerText = '<span>Copyright 2013 - 2019</span> by the <a href="https://www.explorviz.net" target="_blank">ExplorViz project</a>'



// BEGIN aggregated Javadoc in multiproject Gradle build
def exportedProjects= [
		':analysis-service',
		':broadcast-service',
		':discovery-service',
		':history-service',
		':kiekeradapter',
		':landscape-service:landscape-model',
		':landscape-service',
		':settings-service:settings-model',
		':settings-service',
		':user-service',
        ':user-service:user-model'
]

task alljavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("./docs")
    title = 'explorviz-backend API ' + projectVersion
    options.setFooter(footerText)
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
}




// END aggregated Javadoc in multiproject Gradle build
subprojects {


  // BEGIN QA Tooling for subprojects
  
  buildscript {
    repositories {
        jcenter()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.spotbugs:spotbugs-gradle-plugin:2.0.1"
    }
  }
  
  apply plugin: 'pmd'
  apply plugin: 'checkstyle'
  apply plugin: 'com.adarshr.test-logger'
  apply plugin: 'eclipse'
	
  pmd {
    ruleSets = []
    ruleSetFiles = files("../conf/pmd.xml")
    ignoreFailures = true
    toolVersion = "6.7.0"
  }
	
  checkstyle {
    configDir = file("../conf")
    configFile = file("../conf/checkstyle.xml")
    maxWarnings = 0
    ignoreFailures = true
    toolVersion = "8.12"
  }	
	
  task checkstyle {
    group 'Quality Assurance'
    description 'Run Checkstyle'
    
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
  }
	
  task pmd {
    group 'Quality Assurance'
    description 'Run PMD'
    
    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
  }

  sourceSets {
    integrationTest {
      java.srcDir file('src/integrationTest/java')
      compileClasspath += sourceSets.main.output
      runtimeClasspath += sourceSets.main.output
    }
    
    apiTest {
      java.srcDir file('src/apiTest/java')
      compileClasspath += sourceSets.main.output
      runtimeClasspath += sourceSets.main.output
    }
  }
  
  task integrationTest(type: Test) {
    useJUnitPlatform{
      excludeTags 'slow'
      includeEngines 'junit-jupiter'
    }
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
  }
  
  task apiTest(type: Test) {
    useJUnitPlatform{
      excludeTags 'slow'
      includeEngines 'junit-jupiter'
    }
    description = 'Runs api tests.'
    group = 'verification'

    testClassesDirs = sourceSets.apiTest.output.classesDirs
    classpath = sourceSets.apiTest.runtimeClasspath
  }
  
  configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
    
    integrationTestImplementation.setCanBeResolved(true)
    integrationTestRuntimeOnly.setCanBeResolved(true)
    
    apiTestImplementation.extendsFrom implementation
    apiTestRuntimeOnly.extendsFrom runtimeOnly
    
    apiTestImplementation.setCanBeResolved(true)
    apiTestRuntimeOnly.setCanBeResolved(true)
  }

  eclipse {
    classpath {
        plusConfigurations.add configurations.integrationTestRuntimeOnly
        plusConfigurations.add configurations.integrationTestImplementation
        
        plusConfigurations.add configurations.apiTestRuntimeOnly
        plusConfigurations.add configurations.apiTestImplementation
    }
  }
  
  


  // END QA Tooling for subprojects

  repositories {
    // Order of declaration determines how Gradle will check for dependencies at runtime. 
    jcenter()    
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }    
  }
    
  dependencies {

	// SL4J
	implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
    
    // Apache Commons
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    
    
    // Kafka
    implementation group: 'org.apache.kafka', name: 'kafka_2.12', version: '2.2.0'


    
    if (project.name == 'user-service' || project.name == 'discovery-service' || project.name == 'settings-service' || project.name == 'history-service' || project.name == 'broadcast-service') {
    	// subprojects, which are supposed to be a JAX-RS-based microservice
    
    	// Jersey and Servlet
		implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version:'2.27'
		implementation group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.28'
		implementation group: 'javax.servlet', name: 'javax.servlet-api', version:'3.0.1'
		implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version:'2.27'
		testImplementation group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-jetty', version: '2.27'
		
	  
	    // JSON + Parsing
	    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version:'2.27'
	    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version:'2.9.6'
	    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.9.6'
	    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.9.6'

        implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
        implementation group: 'javax.activation', name: 'activation', version: '1.1.1'
        implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.2'
	    
	    // JSON-API parsing
        implementation group: 'com.github.jasminb', name: 'jsonapi-converter', version:'0.9'
            
	    // Jetty servlet-container
		implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-servlet', version: '2.27'
		
		// Database
		implementation group: 'org.mongodb', name: 'mongo-java-driver', version: '3.9.1'
	}
	
	
	
	// Testing and Mocking
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
	testImplementation 'org.junit.platform:junit-platform-runner:1.4.0'
	testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.4.0'
	testImplementation 'org.mockito:mockito-core:2.24.0'	
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.27.0'

    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
    integrationTestImplementation 'org.junit.platform:junit-platform-runner:1.4.0'
    
    apiTestImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
    apiTestImplementation 'org.junit.platform:junit-platform-runner:1.4.0'
  }
  
  test {
    useJUnitPlatform {
        excludeTags 'slow'
        includeEngines 'junit-jupiter', 'junit-vintage'
    }
    failFast = true
  }

}

task apiTest
apiTest.dependsOn subprojects*.assemble

task integrationTest
integrationTest.dependsOn subprojects*.assemble
