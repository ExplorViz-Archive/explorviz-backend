plugins {
    id 'com.adarshr.test-logger' version '1.6.0' apply false
}

allprojects {
  apply plugin: 'java-library'
}

// defining project properties
def	projectVersion = 'v1.3.0'
def projectURL= 'https://www.explorviz.net'
def footerText = '<span>Copyright 2013 - 2019</span> by the <a href="https://www.explorviz.net" target="_blank">ExplorViz project</a>'

// BEGIN aggregated Javadoc in multiproject Gradle build
def exportedProjects= [
		':landscape-service',
		':kiekeradapter',
		':user-service',
		':analysis-service',
		':discovery-service',
		':history-service'
]

task alljavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("./docs")
    title = 'explorviz-backend API ' + projectVersion
    options.setFooter(footerText)
}


// END aggregated Javadoc in multiproject Gradle build
subprojects {


  // BEGIN QA Tooling for subprojects
  
  buildscript {
    repositories {
        jcenter()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.3"
    }
  }
  
  apply plugin: 'pmd'
  apply plugin: 'checkstyle'
  apply plugin: 'com.adarshr.test-logger'
	
  pmd {
    ruleSets = []
    ruleSetFiles = files("../conf/pmd.xml")
    ignoreFailures = true
    toolVersion = "6.7.0"
  }
	
  checkstyle {
    configDir = file("../conf")
    configFile = file("../conf/checkstyle.xml")
    maxWarnings = 0
    ignoreFailures = true
    toolVersion = "8.12"
  }	
	
  task checkstyle {
    group 'Quality Assurance'
    description 'Run Checkstyle'
    
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
  }
	
  task pmd {
    group 'Quality Assurance'
    description 'Run PMD'
    
    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
  }
	
  // END QA Tooling for subprojects

  repositories {
    // Order of declaration determines how Gradle will check for dependencies at runtime. 
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
  }
    
  dependencies {

	// SL4J
	implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
    
    if (project.name == 'user-service' || project.name == 'landscape-service' || project.name == 'discovery-service' || project.name == 'history-service') {
    	// subprojects, which are supposed to be a JAX-RS-based microservice
    
    	// Jersey and Servlet
		implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version:'2.27'
		implementation group: 'javax.servlet', name: 'javax.servlet-api', version:'3.0.1'
		implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version:'2.27'
		// compile group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-jdk-http', version: '2.27'
		testCompile group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-jetty', version: '2.27'
		
	  
	    // JSON + Parsing
	    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version:'2.27'
	    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version:'2.9.6'
	    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.9.6'
	    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.9.6'
	    
	    // JSON-API parsing
        implementation group: 'com.github.jasminb', name: 'jsonapi-converter', version:'0.9'
            
	    // Jetty servlet-container
		implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-servlet', version: '2.27'
		
		// Database
		compile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.9.1'
	}
	
	// Testing and Mocking
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
	testImplementation 'org.junit.platform:junit-platform-runner:1.4.0'
	testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.4.0'
	testImplementation 'org.mockito:mockito-core:2.24.0'	
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.27.0'
  }
  
  test {
    useJUnitPlatform {
        excludeTags 'slow'
        includeEngines 'junit-jupiter', 'junit-vintage'
    }
    failFast = true
  }
    
}