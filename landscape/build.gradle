apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'org.sonarqube'

def loadProperties = {
    def properties = new Properties()
    file('src/main/webapp/WEB-INF/classes/explorviz.properties').withInputStream {
        properties.load(it)
    }
    return properties
}

buildscript {
	repositories {  		
    	maven {
    		url 'https://plugins.gradle.org/m2/'
    	}
  	}

    dependencies {
		classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.1'
    }
}

repositories {
	maven {url 'https://oss.sonatype.org/content/repositories/snapshots'}
	maven {url 'https://jitpack.io'}
}

dependencies {
  // Any .jar in libs
  implementation fileTree(dir: 'libs', include: '*.jar')
  
  // explorviz-kieker-adapter
  implementation project(':kiekeradapter')
  
  implementation project(':shared')
  
  // SSE context for injection
  implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-sse', version: '2.27'
  
  // ExplorViz Discovery Classes
  implementation 'com.github.ExplorViz:explorviz-discovery-common:master-SNAPSHOT'
    
  // Serialization
  implementation group: 'de.ruedigermoeller', name: 'fst', version:'2.47'
  
  // JSON-API parsing
  implementation group: 'com.github.jasminb', name: 'jsonapi-converter', version:'0.8'
  
  // multipart/form-data type
  implementation group: 'org.glassfish.jersey.media', name:'jersey-media-multipart', version:'2.26'
}

// ##### BEGIN Jitpack section #####

// War plugin typically disables jar build
assemble.dependsOn jar

// https://discuss.gradle.org/t/deploying-multiple-artifacts-war-jar-sources-to-artifactory/4239
artifacts {
  	archives file: file('libs/explorviz-common-1.0.jar'), name: 'explorviz-common', type: 'jar'
  	archives file: file('libs/explorviz-worker-1.0.jar'), name: 'explorviz-worker', type: 'jar'
  	archives jar
  	archives war {
        classifier = 'deployment'
    }
}

jar {
  baseName = project.name 
}

install {
  repositories {
    mavenInstaller {
      addFilter('explorviz-common') {artifact, file ->
        artifact.name == 'explorviz-common'
      }
      addFilter('explorviz-worker') {artifact, file ->
        artifact.name == 'explorviz-worker'
      }
      addFilter('explorviz-backend') {artifact, file ->
        artifact.name == 'explorviz-backend'
      }
    }
  }
}

// ##### END Jitpack section #####

// QS Tooling
// will pass until fixing is done
pmd {	
	// Clear the rule set first. Otherwise we would have a lot of additional rules in our rule set.
	ruleSets = []
	ruleSetFiles = files("conf/pmd.xml")
	consoleOutput = false
	ignoreFailures = true
}
    
checkstyle {
	showViolations = false
	configFile = file("conf/checkstyle.xml")
	ignoreFailures = true
}

findbugs {
	reportLevel = "low"
	effort = "max"
	excludeFilter = file("conf/findbugs-exclude.xml")
	ignoreFailures = true
}

tasks.withType(Pmd) {
	reports {
    	xml.enabled = true
        html.enabled = false
    }
}

tasks.withType(FindBugs) {
	reports {
    	xml.enabled = true
        html.enabled = false
    }
}

tasks.withType(Checkstyle) {
  	reports {
    	xml.enabled = true
        html.enabled = false
  }
}

// Javadoc settings
javadoc {
	source = sourceSets.main.allJava
	  destinationDir = file("./docs/")
	  failOnError false
}

// Embedded Webserver
gretty {
	httpPort = loadProperties().httpPort.toInteger()
	servletContainer = loadProperties().servletContainer
	contextPath = loadProperties().contextPath
	recompileOnSourceChange = false
  	reloadOnClassChange = false
  	reloadOnConfigChange = false
  	reloadOnLibChange = false
	// debugging JVM GC - for http://gceasy.io/
	// jvmArgs = [ '-XX:+PrintGCDetails', '-XX:+PrintGCDateStamps', '-Xloggc:gc_jvm.log' ]
}
