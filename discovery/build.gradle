plugins {
	id "application"
	id "com.github.johnrengelman.shadow" version "4.0.4"
	id "com.github.spotbugs" version "1.6.10"
}

// Force new Dependencies 
configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
  // ExplorViz Discovery Classes
  implementation 'com.github.explorviz.explorviz-backend-shared:config-injection:-SNAPSHOT'

  // ExplorViz Discovery Classes
  implementation 'com.github.explorviz.explorviz-backend-shared:discovery-common:-SNAPSHOT'
  
  // ExplorViz Discovery Classes
  implementation 'com.github.explorviz.explorviz-backend-shared:security:-SNAPSHOT'
  
  // ExplorViz common exception handling
  implementation 'com.github.explorviz.explorviz-backend-shared:exception-handling:-SNAPSHOT'
  
  implementation 'com.github.explorviz.explorviz-backend-shared:common-concerns:-SNAPSHOT'
  
  // SSE context for injection
  implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-sse', version: '2.27'  
}

assemble.dependsOn shadowJar
jar.enabled = false

jar {
  manifest {
    attributes(
      'Main-Class': 'net.explorviz.discovery.server.main.Main'
    )
  }
}

application {
    mainClassName = 'net.explorviz.discovery.server.main.Main'
}

shadowJar {
  archiveName = "explorviz-${project.name}.${extension}"
}

spotbugs {
  excludeFilter = file("../conf/spotbugs-exclude-filter.xml")
  reportLevel = "low"
  effort = "max"
  ignoreFailures = true
  toolVersion = '3.1.7'
}

// Per default XML reports for SpotBugs are generated
// Include this to generate HTML reports
tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    // Either HTML or XML reports can be activated
    html.enabled true
    xml.enabled false
  }
}

task spotbugs {
  group 'Quality Assurance'
  description 'Run SpotBugs'
  
  dependsOn 'spotbugsMain'
  dependsOn 'spotbugsTest'
}

artifacts {
  archives shadowJar
}
